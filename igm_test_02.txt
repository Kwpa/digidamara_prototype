Let's make a game!
  name:Digidamara_1
  author:BAGEL+BALLOON and friends
  desc:A game about the end of the universe
  created:03/12/2021
  version:1

Settings

  background:https://ak.picdn.net/shutterstock/videos/1014299564/thumb/1.jpg

CSS
  #box-main
  {
  	left:0px;
  	top:0px;
  	bottom:0px;
    right:0px;
  	overflow:hidden;
  	width:100%;
  	position:absolute;
    }
  #box-logBox
  {
    top:70%;
    width:50%;
    height:30%;
  }
  .teams
  {
    height:200%
  }
  .team1
  {
    position:absolute;
    top:30%;
    left:20%
  }
  .team2
  {
    position:absolute;
    top:30%;
    left:50%
  }
  .team3
  {
    position:absolute;
    top:30%;
    left:80%
  }

  .thing-icon,.particle
  {
    width:200px;
    height:200px;
  }

Layout
  *logBox
    contains:Log
    class:bottom

  *main
    contains:res, buttons, logBox
  *res
    contains:Resources
    class:fullWidth
  *buttons
    contains:Buttons

Includes

  *include teaminit %teamid %teamname %health="100" %refrhealth="50" %lives="3" %maxhealth="100" %healthboost="100" %healthloss="10" %clickcost="1" %sparkle="2"
  *[%teamid]health
    name: [%teamname]
    on start:
      start with:[%health]
    end
    on tick:
      if (teamsurvivalcount>=1)
        lose [%teamid]healthloss [%teamid]health
        if ([%teamid]health<0.5 and [%teamid]lives>=1)
          lose 1 [%teamid]lives
          log [n:this] has lost a life. [[%teamid]lives-1] lives remaining!
          [%teamid]health is [%refrhealth]
          end
        end
    end
  *[%teamid]dead
    on start:
      start with:0
    end
  *[%teamid]lives
    on start:
      start with:[%lives]
    end
    on tick:
      if ([%teamid]lives=0 and [%teamid]dead=0 and teamsurvivalcount>=2)
        lose 1 teamsurvivalcount
        log No lives left. [%teamname] are out of the competition!
        [%teamid]dead is 1
      end
      if (teamsurvivalcount=1 and [%teamid]dead=0)
        log All contestants eliminated! [%teamname] wins!
        [%teamid]dead is 2
      end
    end
  *[%teamid]maxhealth
    on start:
      start with:[%maxhealth]
    end
  *[%teamid]healthboost
    on start:
      start with:[%healthboost]
    end
  *[%teamid]healthloss
    on start:
      start with:[%healthloss]
    end
  *[%teamid]clickcost
    on start:
      start with:[%clickcost]
    end
  *[%teamid]sparkle
    on start:
      start with:[%sparkle]
    end

  *include teambuttoninit %teamid %teamname
    *[%teamid]button
      name:[%teamname]
      desc:Click [%teamname] to generate Sparks, costs Energy
      class:[%teamid] team
      icon:https://static.thenounproject.com/png/788246-200.png
      tooltip origin:bottom
      tooltip class:red
      text:[%teamname] <//>(LIVES = [[%teamid]lives])<//>(HP = [[%teamid]health]/[[%teamid]maxhealth])
      on click:
        if (energy>=1 and [%teamid]health>=1)
          yield [%teamid]sparkle sparks
          yield [%teamid]healthboost [%teamid]health
          lose [%teamid]clickcost energy
          if ([%teamid]health > [%teamid]maxhealth)
            [%teamid]health is [%teamid]maxhealth
          end
        end
      end

  *include schedevent %schedtime %eventname %amessage %bmessage="null" %prob="100%"
    on tick:
      if (eventtime=[%schedtime])
        if (chance([%prob])=1)
          log [%eventname] [%amessage]
        else
          log [%eventname] [%bmessage]
        end
      end
    end

  *include cndevent %condition %eventname %amessage %bmessage="null" %abprob="100%"
    *[%eventname]
    start with:1
    on tick:
      if ([%condition] and [%eventname]=1)
        if (chance([%abprob])=1)
          log [%eventname] [%amessage]
        else
          log [%eventname] [%bmessage]
        end
        this is 0
      end
    end

  *include cndtoggevent %acondition %bcondition %eventname %aaction %baction
    *[%eventname]
    start with:0
    on tick:
      if ([%acondition] and [%eventname]=0)
        [%aaction]
        this is 1
      else if ([%bcondition] and [%eventname]=1)
        [%baction]
        this is 0
      end
    end

    // example [i:cndtoggevent %acondition="team1health<=50" %bcondition="team1health>50" %eventname="cndtoggevent1" %aaction="yield 100 energy" %baction="yield 200 sparks"]


Resources

// time

  *eventtime
    on tick:
      yield 1 eventtime
    end

    // !! put all scheduled events here //

    [i:schedevent %schedtime="2" %eventname="event1" %amessage="<b>Welcome to DIGIDAMARA!</b>" %bmessage="<b>Welcome back to DIGIDAMARA!</b>" %prob="50%"]
    [i:schedevent %schedtime="4" %eventname="Event mega" %amessage="Click on your favourite team to keep them alive!"]
    [i:schedevent %schedtime="6" %eventname="Event mega" %amessage="You have a limited number of clicks (called energy!)"]

// conditionals

  // !! put all conditional events here //

  [i:cndevent %condition="energy<=5" %eventname="cndevent1" %amessage="Running out of energy!"]

// teams

  [i:teaminit %teamid="team1" %teamname="Team 1"]
  [i:teaminit %teamid="team2" %teamname="Team 2"]
  [i:teaminit %teamid="team3" %teamname="Team 3"]

  *teamsurvivalcount
    on start:
      start with:3
    end

// main resources

  *energy
    name:Energy
    desc:Give your energy to the teams
    on start:
  		start with:10
  	end

  *spark|sparks
    name:Spark|Sparks
    desc:Dancers at the end of the universe generate this!

Buttons

  [i:teambuttoninit %teamid="team1" %teamname="Team One"]
  [i:teambuttoninit %teamid="team2" %teamname="Team Two"]
  [i:teambuttoninit %teamid="team3" %teamname="Team Three"]
